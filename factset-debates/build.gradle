buildscript {
	repositories {
		maven {
			url 'http://repository.jsweet.org/artifactory/libs-release-local'
			allowInsecureProtocol true
			content {
				includeGroup 'org.jsweet'
				includeGroup 'org.jsweet.ext'
			}
		}
		mavenCentral()
	}
	dependencies {
		classpath 'org.mytake.mtdo:mtdo-plugin-gradle'
	}
}
apply plugin: 'org.mytake.factset'

mtdoFactset {
	video {
		json { meta ->
			// enforce that every fact has a title like "1 of 2"
			def matcher = meta.fact.title =~ /\((\d) of (\d)\)/
			assert(matcher.find())
			// find the last names of the candidates, sort them, and join with ', '
			String lastNames = meta.speakers.stream()
					.filter({ speaker -> speaker.role.contains("for President") })
					.map({ speaker ->
						int lastSpace = speaker.fullName.lastIndexOf(' ')
						return speaker.fullName.substring(lastSpace + 1)
					})
					.sorted().collect(java.util.stream.Collectors.joining(", "))
			// combine the names and titles
			meta.fact.title = "Presidential Debate - " + lastNames + " (" + matcher.group(1) + " of " + matcher.group(2) + ")"
		}
	}
}
