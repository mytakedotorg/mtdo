plugins {
	id 'com.diffplug.blowdryer'
	id 'com.diffplug.eclipse.resourcefilters'
	id 'com.diffplug.spotless'
}

gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

/* Not compatible with Gradle 5: https://github.com/mmalohlava/gradle-visteg/issues/12
 // set enabled to true to get a report.  Only needed if the task graph
 // changes, see build_deps.pdf for an example
 // dot -Tpdf build/reports/visteg.dot > output.pdf
 visteg {
 enabled = false
 nodeShape = 'box'
 startNodeShape = 'box'
 endNodeShape = 'box'
 colorscheme = 'pastel24' // http://www.graphviz.org/doc/info/colors.html
 }
 */

subprojects {
	apply plugin: 'org.mytake.gradle.jsweetrepo'
	// reproducible builds (for better buildcache)
	project.tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
		dirMode = 0775
		fileMode = 0664
	}
}

// format build files
spotless {
	groovyGradle {
		target 'build.gradle', '*/build.gradle'
		greclipse().configFile(
				'gradle/spotless.eclipseformat.xml',
				'gradle/spotless.greclipse.properties'
				)
		toggleOffOn()
	}
}

// create eclipse project for all projects
allprojects { proj ->
	apply plugin: 'eclipse'
	eclipse { project { name = "mytake-$proj.name" } }
	apply plugin: 'com.diffplug.eclipse.excludebuildfolder'
	tasks.eclipse.dependsOn(tasks.cleanEclipse)
}

// root eclipse project
eclipseResourceFilters {
	exclude().folders().name('buildSrc')
	exclude().folders().name('client')
	exclude().folders().name('client-interface')
	exclude().folders().name('factset-tooling')
	exclude().folders().name('ide')
	exclude().folders().name('lucene')
	exclude().folders().name('node')
	exclude().folders().name('server')
}
tasks.eclipse.dependsOn(tasks.cleanEclipse)

// heroku deploys fatjar (and deletes everything else)
task stage {
	dependsOn ':server:shadowJar', ':lucene:assemble'
	dependsOn ':node:assemble', ':node:pruneDependencies', ':node:copyNodeExe'
	doLast {
		delete fileTree(dir: '.', exclude: [
			'Procfile',
			'app.json',
			'system.properties',
			'.profile.d/**',
			'.jdk/**',
			'.heroku/**',
			'server/build/libs/server-all.jar',
			'lucene/build/search-index',
			'node/build/dist/**',
			'node/build/dist-client/**',
			'node/node',
			'node/node_modules/**'
		])
	}
}
