buildscript { repositories { mavenCentral() }}
plugins {
	id 'com.diffplug.gradle.eclipse.resourcefilters' version '3.17.6'
	id 'com.diffplug.gradle.spotless' version '3.15.0'	// https://github.com/diffplug/spotless/blob/master/plugin-gradle/README.md
	id 'com.github.ben-manes.versions' version '0.20.0'	// https://github.com/ben-manes/gradle-versions-plugin './gradlew dependencyUpdates'
}

/* Not compatible with Gradle 5: https://github.com/mmalohlava/gradle-visteg/issues/12
// set enabled to true to get a report.  Only needed if the task graph
// changes, see build_deps.pdf for an example
// dot -Tpdf build/reports/visteg.dot > output.pdf
visteg {
	enabled = false
	nodeShape = 'box'
	startNodeShape = 'box'
	endNodeShape = 'box'
	colorscheme = 'pastel24' // http://www.graphviz.org/doc/info/colors.html
}
*/

// format build files
spotless {
	groovyGradle {
		target 'build.gradle', '*/build.gradle'
		greclipse().configFile(
				'gradle/spotless.eclipseformat.xml',
				'gradle/spotless.greclipse.properties'
				)
		paddedCell()
	}
}

// create eclipse project for all projects
allprojects { proj ->
	buildscript { repositories { mavenCentral() } }
	apply plugin: 'eclipse'
	eclipse { project { name = "mytake-$proj.name" } }
	apply plugin: 'com.diffplug.gradle.eclipse.excludebuildfolder'
	apply plugin: 'com.diffplug.gradle.eclipse.classic'
	tasks.eclipse.dependsOn(tasks.cleanEclipse)
}

// root eclipse project
eclipseResourceFilters {
	exclude().folders().name('buildSrc')
	exclude().folders().name('ide')
	exclude().folders().name('client')
	exclude().folders().name('server')
	exclude().folders().name('lucene')
	exclude().folders().name('foundation')
	exclude().folders().name('foundation-gen')
}
tasks.eclipse.dependsOn(tasks.cleanEclipse)

// heroku deploys fatjar (and deletes everything else)
task stage {
	dependsOn ':server:shadowJar'
	doLast {
		delete fileTree(dir: '.', exclude: [
			'Procfile',
			'app.json',
			'system.properties',
			'.profile.d/**',
			'.jdk/**',
			'.heroku/**',
			'server/build/libs/server-all.jar'
		])
	}
}
