////////////////////////////////////////////////////////////
// terrible module system, we copy the common and java2ts //
// directories from the client/src/main/scripts folders   //
// here into the node project                             //
////////////////////////////////////////////////////////////
tasks.register('syncClientUtils', Sync) {
	from '../client/src/main/scripts/common'
	into 'src/main/scripts/common'
}

tasks.register('syncClientJava2Ts', Sync) {
	dependsOn ':client:jsweet'
	from '../client/src/main/scripts/java2ts'
	into 'src/main/scripts/java2ts'
}

tasks.register('beforeCompile') {
	dependsOn 'syncClientUtils', 'syncClientJava2Ts'
}

apply plugin: 'com.diffplug.gradle.spotless'
spotless {
	ratchetFrom 'origin/master'
	format 'typescript', {
		licenseHeaderFile rootProject.file('gradle/spotless-license-agpl.java'), '(import|const|declare|export|var) '
		target 'src/main/scripts/**/*.ts'
		targetExclude 'src/main/scripts/java2ts/**', 'src/main/scripts/common/**', 'src/main/scripts/server.ts', '**/__snapshots__/**'
		prettier(VER_PRETTIER).config(['parser': 'typescript'])
	}
	format 'js', {
		target 'gulpfile.js'
		prettier(VER_PRETTIER).config(['parser': 'typescript'])
	}
}

/////////////////////////////////
// smoke test the node project //
/////////////////////////////////
apply plugin: 'org.mytake.gradle.node'
node {
	// node 10.x is needed for node-canvas
	setup.nodeVersion = buildsrc.NvmRc.read(file('../.nvmrc'))
	setup.npmVersion = 'provided'

	npmRun('test_ci') {
		dependsOn 'beforeCompile'
		inputs.dir('src/main/scripts')
		outputs.dir('build/test-results/jest')
	}
}
tasks.register('test') {
	dependsOn 'npm_run_test_ci'
}
tasks.named('check') {
	dependsOn 'test'
}
