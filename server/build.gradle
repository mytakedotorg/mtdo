plugins {
	id 'nu.studer.rocker' version '1.0.1'					// https://github.com/etiennestuder/gradle-rocker-plugin
	id 'com.github.johnrengelman.shadow' version '5.2.0'	// http://imperceptiblethoughts.com/shadow/#
}

apply plugin: 'java'
apply plugin: 'com.diffplug.gradle.spotless'
spotless {
	java {
		target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
		licenseHeaderFile(rootProject.file('gradle/spotless.license.java'))
		importOrder()
		eclipse().configFile(rootProject.file('gradle/spotless.eclipseformat.xml'))
		removeUnusedImports()
		// see common.IpGetter
		custom 'useIpGetter', {
			if (it.contains('req.ip()') && !it.contains('interface IpGetter {')) {
				throw new Error('Use IpGetter instead of Request.ip()')
			}
		}
	}
}

////////////////
// flywayJooq //
////////////////
apply plugin: 'org.mytake.gradle.flywayjooq'
flywayJooq {
	setup.dockerComposeFile = file('src/test/resources/docker-compose.yml')
	setup.dockerConnectionParams = file('build/pgConnection.properties')
	setup.flywayMigrations = file('src/main/resources/db/migration')
	setup.flywaySchemaDump = file('build/schema.sql')

	generator {
		database.inputSchema = 'public'
		generate.pojos = true
		generate.fluentSetters = true
		generate.javaTimeTypes = false
		target.packageName = 'db'
		target.directory = 'src/main/jooq-generated'
	}
	// https://www.jooq.org/doc/latest/manual/code-generation/custom-data-type-bindings/
	forcedType {
		userType = 'java.lang.String'
		binding = 'db.bindings.PostgresInetBinding'
		expression = '.*'
		types = 'inet'
	}
}
// because docker-compose can't start postgres on Heroku
if (buildsrc.Env.isHeroku()) {
	tasks.named('jooq') {
		enabled = false
	}
}

sourceSets.main.java {
	srcDir 'src/main/jooq-generated'
	srcDir 'src/main/rocker'
	srcDir 'src/main/jsoniter-generated'
}

////////////
// rocker //
////////////
rockerVersion = '1.3.0'
rocker {
	main {
		// if these change, also change DevHotReload
		optimize = buildsrc.Env.isHerokuOrCI()
		templateDir = file('src/main/rocker')
		outputDir = file('src/main/rocker-generated')
		extendsModelClass = 'common.CustomRockerModel'
		extendsClass = 'common.CustomRockerTemplate'
	}
}

// enforce tab indentation on rocker files
spotless {
	format 'rocker', {
		target 'src/main/rocker/**/*.rocker.html'
		indentWithTabs(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
}

//////////////////
// normal stuff //
//////////////////
tasks.named('eclipse') {
	dependsOn 'cleanEclipse', 'jooq', 'compileRocker'
}

repositories {
	maven {
		url 'https://dl.bintray.com/palantir/releases'
		content {
			includeGroupByRegex 'com\\.palantir\\..*'
		}
	}
}
dependencies {
	String JOOBY_VER = '1.6.7'
	// jooby
	implementation "org.jooby:jooby-netty:$JOOBY_VER"
	implementation "org.jooby:jooby-rocker:$JOOBY_VER"
	implementation "org.jooby:jooby-jooq:$JOOBY_VER"
	implementation "org.jooby:jooby-commons-email:$JOOBY_VER"
	implementation "org.jooby:jooby-assets:$JOOBY_VER"
	implementation "org.jooby:jooby-quartz:$JOOBY_VER"
	testImplementation "org.jooby:jooby-whoops:$JOOBY_VER"

	// json
	implementation "com.jsoniter:jsoniter:$VER_JSONITER"
	implementation project(':client')
	implementation project(':foundation')
	implementation project(':lucene')

	// jooq (version is set by jooq DSL)
	implementation 'org.jooq:jooq'
	// db connector for postgres and h2
	implementation "org.postgresql:postgresql:${VER_POSTGRESQL_DRIVER}"
	// flyway
	implementation "org.flywaydb:flyway-core:${VER_FLYWAY}"
	// common libs
	implementation 'commons-validator:commons-validator:1.6'
	implementation 'com.diffplug.durian:durian-core:1.2.0'
	implementation 'commons-codec:commons-codec:1.14'

	// code quality annotations, especially @Nullable
	annotationProcessor "com.google.code.findbugs:jsr305:${VER_JSR_305}"
	annotationProcessor 'com.google.errorprone:error_prone_annotations:2.4.0'

	testImplementation "junit:junit:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
	testImplementation 'io.rest-assured:rest-assured:3.1.0'
	testImplementation 'com.icegreen:greenmail:1.5.7'									// http://www.icegreen.com/greenmail/
	testImplementation "com.palantir.docker.compose:docker-compose-rule-core:$VER_PALANTIR_DOCKER_COMPOSE"		// https://github.com/palantir/docker-compose-rule
	testImplementation "com.palantir.docker.compose:docker-compose-rule-junit4:$VER_PALANTIR_DOCKER_COMPOSE"	// https://github.com/palantir/docker-compose-rule

	testImplementation "com.fizzed:rocker-compiler:${rockerVersion}"					// required for hotreload

	// dep for auth
	implementation 'com.auth0:java-jwt:3.10.3'
}

task runProd(type: JavaExec, dependsOn: classes) {
	description = 'Run app in prod mode'
	classpath = sourceSets.main.runtimeClasspath
	main = 'common.Prod'
	if (project.hasProperty('DATABASE_URL')) {
		environment('DATABASE_URL', project.getProperty('DATABASE_URL'))
	}
	if (project.hasProperty('SPARKPOST_APIKEY')) {
		environment('SPARKPOST_APIKEY', project.getProperty('SPARKPOST_APIKEY'))
	}
	args = [
		'prod'
	]
}

// see common.DevHotReload in an IDE for faster dev
task runDev(type: JavaExec, dependsOn: testClasses) {
	description = 'Run app in dev mode'
	classpath = sourceSets.test.runtimeClasspath
	main = 'common.Dev'
	args = [
		'dev'
	]
}

/////////////
// fat jar //
/////////////
shadowJar {}
